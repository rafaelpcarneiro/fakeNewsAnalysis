# vim: set foldmethod=marker:
# GNUPLOT script
# tricks: (stats) AND (show variable all)
# tricks: (test) show all lt and colours 
reset

diagramOutFile     = "pphDiagramPlot.png"
BarCodeOutFileDim0 = "pph0_BarCode.png"
BarCodeOutFileDim1 = "pph1_BarCode.png"

pph0InFile         = "data/pph0.txt"
pph1InFile         = "data/pph1.txt"

### Global variables
stats        pph0InFile u 2
yDim0Max     = STATS_max
dataSizeDim0 = STATS_records

stats        pph1InFile u 2
yDim1Max     = STATS_max
dataSizeDim1 = STATS_records

myINFTY = yDim0Max < yDim1Max ? 1.2 * yDim1Max : 1.2 * yDim0Max

### styles
set style line 1 pt 2  ps 1.5 lc rgb "black" #blue
set style line 2 pt 2  ps 1.5 lc rgb "#ff0000" #red
set style line 3 lt 1  lw 1   lc rgb "#0000ff"  
set style line 4              lc rgb "#0000b3" #red

# Ploting the Diagrams {{{1

set terminal pngcairo size 800,700 enhanced font 'verdana,11'
set output diagramOutFile

diag(x) = x                         ## diagonal function
f(x)    = x > -1 ? x: myINFTY       ## function to set -1 to INFTY

# ranges
set xrange [-1:myINFTY+1]
set yrange [-1:myINFTY+1]

set key bottom right

set border 3
set tics nomirror

#set xtics format ""
set grid ytics 

set label 1 "∞" at 0,myINFTY left  offset -4,0 font 'verdana,18'

plot pph0InFile u 1:(f($2)) ls 1 t 'pph dim = 0', \
     pph1InFile u 1:(f($2)) ls 2 t 'pph dim = 1', \
     [x = 0:myINFTY] diag(x) ls 3 notitle

# END Ploting the Diagrams 1}}}

# Ploting the Bar Codes -- Dimension 0 {{{1

set terminal pngcairo size 800,700 enhanced font 'verdana,11'
set output  BarCodeOutFileDim0

set style fill solid 0.8
set boxwidth 0.95 relative

# ranges
set xrange [-100 : dataSizeDim0 + 100] 
set yrange [-1   : yDim0Max + 1      ] 

set nokey 

set tics  nomirror
set xtics rotate
set ytics rotate

set ylabel "Comprimento" rotate

set grid ytics 

set label 1 "∞" at 0,yDim0Max right  offset -3.2,0.5 font 'verdana,18' rotate

plot pph0InFile u ($2 == -1 ? yDim0Max: $2 -$1) w boxes ls 4

# END Ploting the Bar Codes -- Dimension 0 1}}}

# Ploting the Bar Codes -- Dimension 1 {{{1

set terminal pngcairo size 800,700 enhanced font 'verdana,11'
set output  BarCodeOutFileDim1

set style fill solid 0.8
set boxwidth 0.95 relative

# ranges
set xrange [-100 : dataSizeDim1 + 100] 
set yrange [-1   : yDim1Max + 1      ] 

set nokey 

set tics  nomirror
set xtics rotate
set ytics rotate

set ylabel "Comprimento" rotate

set grid ytics 

set label 1 "∞" at 0,yDim1Max right  offset -3.2,0.5 font 'verdana,18' rotate

plot pph1InFile u ($2 == -1 ? yDim1Max: $2 -$1) w boxes ls 4

# END Ploting the Bar Codes -- Dimension 1 1}}}
